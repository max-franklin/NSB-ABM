
;GLOBAL
globals [
          ;matrices
          hunter-fcm-adja-list
          hunter-fcm-agentnum-list
          hunter-fcm-success-list

          global-fcm-matrix
          global-input-matrix
          global-fcm-sensory
          global-fcm-internal
          global-fcm-motor
          ;prints
          global-full-print
          global-sensory-print
          global-internal-print
          global-motor-print
          
          num-harvests-global
          home-patch-global
        ]

;HUNTERS
hunters-own [
             ;concepts
             prey-close      ;sensory 
             prey-far        
             trip-long
             trip-short 
             boat-and-hike-long
             boat-and-hike-short
             caribou-harvest-low 
             caribou-harvest-high 
             hunter-density-low 
             hunter-density-high
             rivers-in-way
              
             hunting         ;internal
             exploration    
             weakness
             strength   
             off-boat
             on-boat
             lacking
             success 
             
             hunt            ;motor
             foot-travel
             boat-travel       
             return 
             
             ;FCM          
             ;hunter-fcm-internal
             ;hunter-fcm-motor 
             ;hunter-fcm-raw-internal
             ;hunter-fcm-act-internal  
             ;hunter-fcm-sensory-test
             hunter-fcm-motor-results
             hunter-fcm-sensory
             hunter-fcm-matrix
             hunter-input-matrix
             current-activations
             previous-activations
             vector-A
             vector-B
             vector-AB-sum
             
             ;data
             boat-return 
             local-search 
             return-to-river
             prev-motor-state
             distance-to-prey
             trip-length 
             travel-heuristic 
             harvest-amount
             hunter-density 
             number-of-rivers-way
             sensory-prey-close
             sensory-prey-far 
             sensory-trip-long 
             sensory-trip-short 
             sensory-boat-and-hike-long
             sensory-boat-and-hike-short
             sensory-caribou-harvest-low 
             sensory-caribou-harvest-high 
             sensory-hunter-density-low 
             sensory-hunter-density-high 
             sensory-rivers-in-way
             motor-concept-index
             target-river-centroid
             target-land-centroid
             target-river-patch
             target-river-patch-r
             home-centroid
             river-to-follow
             river-memory
             land-p
             prey
             home-patch
             catch-probability
             prey-caught
             harvest-patch
             wetness-sum 
             patch-sum
             boat-speed 
             hike-speed
             spent-time 
             boat-dist
             hike-dist-0
             hike-dist-1
             boat-t
             hike-t-0
             hike-t-1
             hunter-counter
             number-of-trips
             number-of-hunters 
             data-output
             distance-land-centroid
             num-riw
             patch-list-riw
             patch-temp-riw
             x1-riw 
             y1-riw
             x2-riw
             y2-riw  
             slope-riw 
             x-riw 
             y-riw 
             new-trip?
             sensory-print
             internal-print
             raw-internal-print
             act-internal-print
             motor-print
             motor-results-print
             num-rows-h
             num-cols-h
             motor-max
            ]

to setup-hunter-nls
  reset-ticks
  kill-hunters
  initialize-FCM-hunters
  new-hunters
end

to go-hunters-nls
  ask hunters 
  [
    if(trip-length = 0)
    [ 
      if export-hunter-data? [export-hunter-data 0]
      set new-trip? true 
    ] 
    if(new-trip? = true)
    [
     set hunter-counter hunter-trip-control
     while[hunter-counter > 0]
     [
       set hunter-counter hunter-counter - 1
       if (hunter-counter = 0) 
       [ 
         reset-hunters 0 
         set new-trip? false
       ]  
     ]
    ]
    if (new-trip? = false)
    [ execute-FCM ]
  ]
end

;NEW HUNTERS
to new-hunters
  create-hunters hunter-population
  [
    set color white
    set size 3
    setxy 3 1
    set number-of-trips 13
    set boat-speed 30
    set hike-speed 5
    set river-memory []
    set home-patch patch 3 1 
    set trip-length trip-length-max
    set spent-time 0
    set harvest-amount 0
    set hunter-density 0
    set patch-sum 0
    set-hunter-sensory-ranges
    set-initial-centroids
    set prey-caught false
    set return false 
    let done false
    set return-to-river false
    set boat-return false
    set local-search false
    set new-trip? false
    set-hunter-matrices
  ]
  set home-patch-global patch 3 1
end

to reset-hunters [ mode ]
   ;per trip for mode = 0 
   ;per year for mode = 1 
   ifelse (mode = 0)
   [
     set number-of-trips (number-of-trips - 1)
   ]
   [
     set-hunter-matrices
     set number-of-trips 13
     set harvest-amount 0
   ]
   if(patch-here != home-patch) [ setxy 3 1 ] 
   set river-memory []
   set target-river-patch-r 0
   set target-river-patch 0
   set trip-length trip-length-max
   set spent-time 0
   set hunter-density 0
   set patch-sum 0
   set-initial-centroids
   set prey-caught false
   set local-search false
   set return false 
   set boat-return false
   let done false
   set return-to-river false
end

to set-hunter-sensory-ranges 
    set prey-close (prey-close-constant * hunter-vision)
    set prey-far (prey-far-constant * hunter-vision)
    
    set trip-long (trip-long-constant * trip-length-max)
    set trip-short (trip-short-constant * trip-length-max) 
    
    set boat-and-hike-long (boat-hike-long-constant * 1) 
    set boat-and-hike-short (boat-hike-short-constant * 1)
    
    set caribou-harvest-low (caribou-harvest-low-constant * hunter-harvest-goal)
    set caribou-harvest-high (caribou-harvest-high-constant * hunter-harvest-goal)
    
    set hunter-density-low (hunter-density-low-constant * maximum-hunter-density)
    set hunter-density-high (hunter-density-high-constant * maximum-hunter-density)
    
    set rivers-in-way 1
end 


to initialize-FCM-hunters
  set hunter-fcm-adja-list [ ]
  set global-fcm-matrix matrix:from-row-list [[ 0  0  0  0  0  0  0  0  0  0  0  1 -1  0  0  0  0  0  0  0  0  0  0 ]
                                              [ 0  0  0  0  0  0  0  0  0  0  0 -1  1  0  0  0  0  0  0  0  0  0  0 ]
                                              [ 0  0  0  0  0  0  0  0  0  0  0  0  0  1 -1  0  0  0  0  0  0  0  0 ]
                                              [ 0  0  0  0  0  0  0  0  0  0  0  0  0 -1  1  0  0  0  0  0  0  0  0 ]
                                              [ 0  0  0  0  0  0  0  0  0  0  0  0  0  0  0  1 -1  0  0  0  0  0  0 ]
                                              [ 0  0  0  0  0  0  0  0  0  0  0  0  0  0  0 -1  1  0  0  0  0  0  0 ]
                                              [ 0  0  0  0  0  0  0  0  0  0  0  0  0  0  0  0  0 -1  1  0  0  0  0 ]
                                              [ 0  0  0  0  0  0  0  0  0  0  0  0  0  0  0  0  0  1 -1  0  0  0  0 ]
                                              [ 0  0  0  0  0  0  0  0  0  0  0  1  0  0  0  0  0  0  0  0  0  0  0 ]
                                              [ 0  0  0  0  0  0  0  0  0  0  0 -1  1  0  0  0  0  0  0  0  0  0  0 ]
                                              [ 0  0  0  0  0  0  0  0  0  0  0  0  0  0  0 -2  1  0  0  0  0  0  0 ]
                                              [ 0  0  0  0  0  0  0  0  0  0  0  0  0  0  0  0  0  0  0  1 -1  0  0 ]
                                              [ 0  0  0  0  0  0  0  0  0  0  0  0  0  0  0  0  0  0  0  0  1  1  0 ]
                                              [ 0  0  0  0  0  0  0  0  0  0  0  0  0  0  0  0  0  0  0 -1 -1 -1  2 ]
                                              [ 0  0  0  0  0  0  0  0  0  0  0  0  0  0  0  0  0  0  0  1  1  1 -1 ]
                                              [ 0  0  0  0  0  0  0  0  0  0  0  0  0  0  0  0  0  0  0  0  1 -1  0 ]
                                              [ 0  0  0  0  0  0  0  0  0  0  0  0  0  0  0  0  0  0  0  0 -1  1  0 ]
                                              [ 0  0  0  0  0  0  0  0  0  0  0  0  0  0  0  0  0  0  0 -1  0  0  2.5 ]
                                              [ 0  0  0  0  0  0  0  0  0  0  0  0  0  0  0  0  0  0  0  1  0  0 -1 ]
                                              [ 0  0  0  0  0  0  0  0  0  0  0  0  0  0  0  0  0  0  0  0  0  0  0 ]
                                              [ 0  0  0  0  0  0  0  0  0  0  0  0  0  0  0  0  0  0  0  0  0  0  0 ]
                                              [ 0  0  0  0  0  0  0  0  0  0  0  0  0  0  0  0  0  0  0  0  0  0  0 ]
                                              [ 0  0  0  0  0  0  0  0  0  0  0  0  0  0  0  0  0  0  0  0  0  0  0 ]]
  
  set global-input-matrix matrix:from-row-list [[ 1 -1  0  0  0  0  0  0  0  0  0  0  0  0  0  0  0  0  0  0  0  0  0 ]
                                                [-1  1  0  0  0  0  0  0  0  0  0  0  0  0  0  0  0  0  0  0  0  0  0 ]
                                                [ 0  0  1 -1  0  0  0  0  0  0  0  0  0  0  0  0  0  0  0  0  0  0  0 ]
                                                [ 0  0 -1  1  0  0  0  0  0  0  0  0  0  0  0  0  0  0  0  0  0  0  0 ]
                                                [ 0  0  0  0  1 -1  0  0  0  0  0  0  0  0  0  0  0  0  0  0  0  0  0 ]
                                                [ 0  0  0  0 -1  1  0  0  0  0  0  0  0  0  0  0  0  0  0  0  0  0  0 ]
                                                [ 0  0  0  0  0  0  1 -1  0  0  0  0  0  0  0  0  0  0  0  0  0  0  0 ]
                                                [ 0  0  0  0  0  0 -1  1  0  0  0  0  0  0  0  0  0  0  0  0  0  0  0 ]
                                                [ 0  0  0  0  0  0  0  0  1 -1  0  0  0  0  0  0  0  0  0  0  0  0  0 ]
                                                [ 0  0  0  0  0  0  0  0 -1  1  0  0  0  0  0  0  0  0  0  0  0  0  0 ]
                                                [ 0  0  0  0  0  0  0  0  0  0  1  0  0  0  0  0  0  0  0  0  0  0  0 ]]
                                             
  set global-fcm-sensory matrix:from-row-list [[ 0  0  0  0  0  0  0  0  0  0  0]]
end

to set-hunter-matrices
    set hunter-fcm-matrix global-fcm-matrix
    set hunter-fcm-sensory global-fcm-sensory
    set hunter-input-matrix global-input-matrix
    set current-activations  matrix:from-row-list [[0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0]]
    set previous-activations  matrix:from-row-list [[0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0]]
end

to set-hunter-fcm-sensory
  set sensory-prey-close (1 - (ternary-funct prey-close prey-far distance-to-prey))
  set sensory-prey-far  ternary-funct prey-close prey-far distance-to-prey
  
  set sensory-trip-long (1 - (ternary-funct trip-long trip-short trip-length))
  set sensory-trip-short ternary-funct trip-long trip-short trip-length 
  
  set sensory-boat-and-hike-short ternary-funct boat-and-hike-short boat-and-hike-long travel-heuristic 
  set sensory-boat-and-hike-long (1 - (ternary-funct boat-and-hike-short boat-and-hike-long travel-heuristic))
  
  set sensory-caribou-harvest-low (1 - (ternary-funct caribou-harvest-low caribou-harvest-high harvest-amount))
  set sensory-caribou-harvest-high ternary-funct caribou-harvest-low caribou-harvest-high harvest-amount 
  
  set sensory-hunter-density-low (1 - (ternary-funct hunter-density-low hunter-density-high hunter-density))
  set sensory-hunter-density-high ternary-funct hunter-density-low hunter-density-high hunter-density 
  
  set sensory-rivers-in-way ternary-funct 0 rivers-in-way number-of-rivers-in-way
  
  matrix:set hunter-fcm-sensory 0 0 sensory-prey-close
  matrix:set hunter-fcm-sensory 0 1 sensory-prey-far 
  matrix:set hunter-fcm-sensory 0 2 sensory-trip-long
  matrix:set hunter-fcm-sensory 0 3 sensory-trip-short
  matrix:set hunter-fcm-sensory 0 4 sensory-boat-and-hike-long
  matrix:set hunter-fcm-sensory 0 5 sensory-boat-and-hike-short 
  matrix:set hunter-fcm-sensory 0 6 sensory-caribou-harvest-low
  matrix:set hunter-fcm-sensory 0 7 sensory-caribou-harvest-high
  matrix:set hunter-fcm-sensory 0 8 sensory-hunter-density-low
  matrix:set hunter-fcm-sensory 0 9 sensory-hunter-density-high 
  matrix:set hunter-fcm-sensory 0 10 sensory-rivers-in-way
  
  set sensory-print matrix:pretty-print-text hunter-fcm-sensory
end

to set-activations
  set previous-activations current-activations
  set vector-A matrix:times previous-activations hunter-fcm-matrix 
  set vector-B matrix:times hunter-fcm-sensory hunter-input-matrix
  set vector-AB-sum matrix:plus vector-A vector-B
  set current-activations matrix:map logistic-funct vector-AB-sum
end

to set-action
  set hunter-fcm-motor-results matrix:submatrix current-activations 0 19 1 23
  let mat-dims matrix:dimensions hunter-fcm-motor-results
  set num-rows-h item 0 mat-dims
  set num-cols-h item 1 mat-dims
  let i 0
  set motor-concept-index 0
  set motor-max 0
  let matrix-temp 0
  while [ i < num-cols-h ]
  [
     set matrix-temp matrix:get hunter-fcm-motor-results 0 i
     if ( matrix-temp > motor-max )
     [
       set motor-max matrix-temp
       set motor-concept-index i
     ]
     set i ( i + 1 )
  ]
end

to run-motor-function
  if motor-concept-index = 0
  [
    if prev-motor-state = 2 [ save-target-river-patch ] ;save closest river patch 
    set prev-motor-state 0
    set foot-travel false
    set boat-travel false
    set boat-return false
    set return false
    set hunt true
    set local-search false
    hunt-funct
  ]
  if motor-concept-index = 1
  [
    if prev-motor-state = 2 [ save-target-river-patch ] ;save closest river patch 
    set prev-motor-state 1
    set hunt false
    set boat-travel false
    set boat-return false
    set return false
    set foot-travel true
    foot-travel-funct ([patch-here] of target-land-centroid)
  ]
  if motor-concept-index = 2 ;CHECK TO RETURN BACK TO RIVER PATCH 
  [
    set prev-motor-state 2
    set hunt false
    set foot-travel false
    set return false
    set boat-return false
    set boat-travel true
    set local-search false
    ifelse (target-river-patch-r != 0)
    [ boat-travel-funct target-river-patch-r ] ;pass in returning patch 
    [ boat-travel-funct target-river-patch ]
  ]
  if motor-concept-index = 3
  [
    if prev-motor-state = 2 [ save-target-river-patch ]
    set prev-motor-state 3
    set hunt false
    set boat-travel false
    set foot-travel false
    set return true
    set local-search false
    ;if(patch-here = target-river-patch-r) [set boat-return true]
    return-home
  ]
  
end

to execute-FCM
    find-prey
    set-distance-to-prey
    set-travel-cost-evaluation
    set-hunter-density
    set-hunter-fcm-sensory
    set-activations
    set-action
    run-motor-function
    spend-trip-length
end

to hunt-funct
  if(prey != nobody)[ 
  head-towards-prey
  catch-prey
  move-forward
  ]
end

to foot-travel-funct [target]
  if(patch-here = target) [set local-search true] ;IF LOCAL SEARCH FALSE     ;Travel to destination 
  ifelse (local-search = false)
  [
     ifelse(member? target neighbors = false)
     [
        set land-p min-one-of neighbors with [ocean = 0 and (streams < hunter-streams-restriction) and ( [distance target] of self < [distance target] of myself )] [wetness]
        if(land-p != nobody)
        [
           set wetness-sum (wetness-sum + ([wetness] of land-p))
           set patch-sum (patch-sum + 1)
           face land-p
           move-to land-p
        ]
     ]
     [
        set wetness-sum (wetness-sum + ([wetness] of target))
        set patch-sum (patch-sum + 1)
        face target
        move-to target
        ;set local search true
     ]
  ]
  [  ;Local search 
     set land-p (one-of neighbors with [[distance target] of self <= local-search-radius and ocean = 0 and (streams < hunter-streams-restriction)])
     set patch-sum (patch-sum + 1)
     face land-p
     move-to land-p
  ] 
end

to foot-travel-return-funct [target]
     ifelse(member? target neighbors = false)
     [
        set land-p min-one-of neighbors with [ocean = 0 and (streams < hunter-streams-restriction) and ([distance target] of self < [distance target] of myself )] [wetness]
        if(land-p != nobody)
        [
          set wetness-sum (wetness-sum + ([wetness] of land-p))
          set patch-sum (patch-sum + 1)
          face land-p
          move-to land-p
        ]
     ]
     [
        set wetness-sum (wetness-sum + ([wetness] of target))
        set patch-sum (patch-sum + 1)
        face target
        move-to target
     ]
end

to boat-travel-funct [target-r]
  if(empty? [river-set] of patch-here = true) [set return-to-river true]
  if([connected?] of patch-here = true and empty? [river-set] of patch-here = false and ([river-set] of patch-here != [river-set] of target-r)) [set return-to-river true]
  ifelse(return-to-river = false)
  [
       set river-memory lput patch-here river-memory
       let target neighbors with [connected? = true and not member? self [river-memory] of myself 
                             and (list-intersect ([river-to-follow] of myself) river-set) ] 
       set target max-one-of target [elevation]
       carefully [ face target move-to target ]
       [
         move-to min-one-of neighbors with [(list-intersect ([river-to-follow] of myself) river-set)][pycor]
         set target neighbors with [connected? = true and not member? self [river-memory] of myself 
                                and (list-intersect ([river-to-follow] of myself) river-set)] 
         set target max-one-of target [elevation]
         if target != NOBODY
         [ face target move-to target ]
       ]
   ]
   [
     if (target-r = 0)
     [ set target-r home-patch ] 
     foot-travel-return-funct target-r
   ]
end 

to boat-travel-return-funct
  set river-memory []
  set river-memory lput patch-here river-memory
  let target neighbors with [connected? = true and not member? self [river-memory] of myself and ( [distance home-patch-global] of self < [distance home-patch] of myself )
                             and (list-intersect ([river-to-follow] of myself) river-set)] ;in-radius 2 ;and not member of my memory
  set target min-one-of target [elevation]
  carefully [ face target move-to target ];move-to target
  [
     move-to max-one-of neighbors with [(list-intersect ([river-to-follow] of myself) river-set)] [pycor];min-one-of
     ;move-to max-one-of neighbors [pycor]
     set target neighbors with [connected? = true and not member? self [river-memory] of myself and ( [distance home-patch-global] of self < [distance home-patch] of myself )
                                and (list-intersect ([river-to-follow] of myself) river-set)] ;in-radius 2    
     set target min-one-of target [elevation]
     if target != NOBODY
     [ face target move-to target ]
  ]
end 

to-report list-intersect [list1 list2] ;find intersection between riversets 
  foreach list1 [ if (member? ? list2) [report true] ] 
  report false 
end 


to find-prey
   set prey one-of caribou in-radius hunter-vision
end

to set-hunter-density 
  set hunter-density count hunters in-radius hunter-vision 
end

to head-towards-prey
  set heading towards prey 
end

to catch-prey
   if (distance prey <= 0.5)
   [
     set hunter-harvest-total hunter-harvest-total + 1
     set prey-caught true
     set num-harvests-global (num-harvests-global + 1)
     set harvest-amount (harvest-amount + 1)
     set harvest-patch [patch-id] of patch-here
   ]
end

to move-forward
   fd 1
   set wetness-sum (wetness-sum + ([wetness] of patch-here))
   set patch-sum (patch-sum + 1)
end

to-report number-of-rivers-in-way
  set num-riw 0
  if (xcor != [xcor] of target-land-centroid and ycor != [ycor] of target-land-centroid)
  [
    set distance-land-centroid (distance target-land-centroid)
    set x-riw 0 
    while[x-riw <= distance-land-centroid]
    [
      set patch-temp-riw patch-at-heading-and-distance (towards target-land-centroid) x-riw
      if(empty? [river-set] of patch-temp-riw = false or [connected?] of patch-temp-riw = true)
      [set num-riw (num-riw + 1)] 
      set x-riw x-riw + 1
    ]
  ]
  report num-riw
end 

to set-distance-to-prey
   ifelse prey != nobody
   [
     set distance-to-prey distance prey
   ]
   [
     set distance-to-prey (hunter-vision + 1) 
   ]
end

to set-travel-cost-evaluation 
  set boat-dist (distance target-river-patch)                              ;distance to river patch
  set hike-dist-0 (distance target-land-centroid)                          ;distance from me to destination
  set hike-dist-1 [distance [target-land-centroid] of myself] of target-river-patch    ;distance from river patch to destination 
  set boat-t (boat-dist / boat-speed)                                      ;time to boat to river patch
  set hike-t-0 (hike-dist-0 / hike-speed)                                  ;time to hike from me to destination 
  set hike-t-1 (hike-dist-1 / hike-speed) 
  ifelse(hike-t-0 != 0)                                                    ;time to hike from river patch to destination 
  [set travel-heuristic (hike-t-0 - (boat-t + hike-t-1)) / hike-t-0]
  [set travel-heuristic 0]
end

to return-home
  ifelse(target-river-patch-r != 0)
  [
    ;if(patch-here = target-river-patch-r)[set boat-return true]
    ;if(empty? [river-set] of patch-here = true) [set return-to-river true]
    if(patch-here = target-river-patch-r or (empty? [river-set] of patch-here = false and ([river-set] of patch-here = [river-set] of target-river-patch-r))) 
    [set boat-return true]
    ifelse(boat-return = true)
    [
      boat-travel-return-funct
    ]
    [
      foot-travel-return-funct target-river-patch-r
    ]
  ]
  [ 
    foot-travel-return-funct home-patch 
  ]
end

to spend-trip-length
  set trip-length (trip-length - 1)
  set spent-time (spent-time + 1)
end

to print-trip-length 
  print "Trip length:"
  print trip-length   
end 

to check-target-river-centroid ;check the centroid you're at for the target 
   if (member? target-river-centroid caribou-harvests in-radius (.25 * hunter-vision))
   [ 
      ask target-river-centroid [ ht ]  
      set river-memory []
      set target-river-centroid home-centroid
   ]
end

to set-target-land-centroid ;set the land destination 
   set target-land-centroid caribou-harvest (select-weighted-val caribou-harvest-probability-list caribou-harvest-selection-list)
end

to set-target-river
   set target-river-patch min-one-of patches with [connected? = true and empty? river-set = false] [distance [target-land-centroid] of myself]
   set river-to-follow [river-set] of target-river-patch
end

to set-initial-centroids ;initialize centroids 
   set home-centroid min-one-of caribou-harvests [distance myself] 
   set-target-land-centroid 
   set-target-river
end

to save-target-river-patch 
  set target-river-patch-r min-one-of patches with [connected? = true and empty? river-set = false] [distance myself]
  set river-to-follow [river-set] of target-river-patch-r
  set return-to-river true
end

to-report ternary-funct [ low high sense]
  if ( sense < low )
  [ report 0 ]
  if ( sense > high )
  [ report 1 ]
  report (( sense - low ) / ( high - low ))
end

to-report logistic-funct [ x ]
  report ( 1 / ( 1 + e ^ ( -1 * x )))
end

to kill-hunters
  ask hunters [ die ]
end

to update-hunter-fcms
  ;;Code block for evolving hunter FCMs. Selection occurs, followed by recombination and mutation.
  ;;Akin to the caribou FCM evolution code, the hunter 'reset' should probably occur in here, too. 
  
  ;set caribou-fcm-adja-list [fcm-adja] of caribou
  ;;insert here... what the adjacency matrix should be.
  
  set hunter-fcm-adja-list [ hunter-fcm-matrix ] of hunters
  
  ;set caribou-fcm-agentnum-list [ ]
  ;set hunter-fcm-agentnum-list [ ]

  ;set caribou-fcm-success-list [ ]
  ;set hunter-fcm-success-list [ ]
  
  ;;1 for hunters, 0 for caribou
  let fcm-var-out (build-fcm-data hunter-fcm-adja-list  1)
  
  let weighted-list item 0 fcm-var-out
  let prob-list item 1 fcm-var-out
  
  ask hunters
    [
      
      ;print word "previous fcm: " fcm-adja
      ifelse hunter-recombine?
      [ 
        let genome-1 select-weighted-val prob-list hunter-fcm-adja-list
        let genome-2 select-weighted-val prob-list hunter-fcm-adja-list
        ;print word "new fcm: " fcm-adja
        set hunter-fcm-matrix cross-genome genome-1 genome-2 hunter-recomb-prob hunter-fcm-matrix
      ]
      [
        set hunter-fcm-matrix select-weighted-val prob-list hunter-fcm-adja-list
      ]
      
      if hunter-mutate? [ set hunter-fcm-matrix mutate-genome hunter-mutate-prob hunter-fcm-matrix ]
      
      ;;now, can reference "reset" code procedure here to make sure they're all back at Nuiqsut
      ;;for the next year of the simulation.
      reset-hunters 1
    ]
end